def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def signingKeyStorePassword = keystoreProperties.getProperty('storePassword')
if (signingKeyStorePassword == null) {
    signingKeyStorePassword = System.getenv('ANDROID_KEYSTORE_PASSWORD')
}

def signingKeyAlias = keystoreProperties.getProperty('keyAlias')
if (signingKeyAlias == null) {
    signingKeyAlias = System.getenv("ANDROID_KEYSTORE_ALIAS")
}

def signingKeyPassword = keystoreProperties.getProperty('keyPassword')
if (signingKeyPassword == null) {
    signingKeyPassword = System.getenv('ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD')
}

def signingKeyStoreFilePath = keystoreProperties.getProperty('storeFile')
if (signingKeyStoreFilePath == null) {
    signingKeyStoreFilePath = System.getenv("ANDROID_KEYSTORE_PATH")
}
if (signingKeyStoreFilePath == null) {
    signingKeyStoreFilePath = "../island-puzzle.jks"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    namespace "nl.renefloor.puzzle"
    compileSdkVersion 34
    ndkVersion "25.1.8937393"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "nl.renefloor.puzzle"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    // TODO: Remove when below fix is available in stable channel.
    // https://github.com/flutter/flutter/pull/82309
    lintOptions {
        checkReleaseBuilds false
    }

    signingConfigs {
        release {
            storeFile file(signingKeyStoreFilePath)
            storePassword signingKeyStorePassword
            keyAlias signingKeyAlias
            keyPassword signingKeyPassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
